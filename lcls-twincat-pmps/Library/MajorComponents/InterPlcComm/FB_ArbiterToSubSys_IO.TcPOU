<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_ArbiterToSubSys_IO" Id="{1a7d9761-dced-4384-823d-a5e0b037bf80}" SpecialFunc="None">
    <Declaration><![CDATA[(* Exchanges preemption requests and current beam state with subsystems in arbiter
network.

2019-12-2, A. Wallace

*)
FUNCTION_BLOCK FB_ArbiterToSubSys_IO
VAR_INPUT
	RequestingSystemID	:	DWORD := PMPS_GVL.EXCLUDED_ASSERTION_ID; // System ID for making requests to the supreme arbiter
    Reset : BOOL;
END_VAR
VAR_OUTPUT
END_VAR
VAR_IN_OUT
	Arbiter	: FB_Arbiter;
    fbFFHWO : FB_HardwareFFOutput;
END_VAR
VAR
    {attribute 'pytmc' := '
        pv: RequestedBP:FromSubSys
    '}
	i_RequestedBP AT %I*	:	ST_BeamParams_IO; // Requested BP from subsystem
    RequestedBP: ST_BeamParams;
    {attribute 'pytmc' := '
        pv: CurrentBP:Broadcast
    '}
	o_CurrentBP AT %Q* : ST_BeamParams_IO; // Current BP set
    
    // EL6692 Diagnostics
    i_Connected AT %I* : BOOL; // SYNC Inputs^External device not connected !!! Doesn't really work.
    {attribute 'pytmc' := 'pv: WcState
        io: i
        field: DESC Working counter state.
        field: ZNAM OK
        field: ONAM Error'}
    i_WcState AT %I* : BOOL; // WcState^WcState
    {attribute 'pytmc' := 'pv: TxPDO_state
        io: i
        field: DESC PDO Transmission is OK
        field: ZNAM OK
        field: ONAM Error'}
    i_TxPDOState AT %I* : BOOL; // SYNC Inputs^TxPDO state
    {attribute 'pytmc' := 'pv: TxPDO_toggle
        io: i
        field: DESC PDO Transmission is OK
        field: ZNAM OK
        field: ONAM Error'}
    i_TxPDOToggle AT %I* : BOOL; // TxPDO toggle
    
    // Fast faults
    ffPMPSIO_Disconnect : FB_FastFault := (i_Desc:='Fast fault on arbiter disconnection'); // Fast fault for ethercat issues
    nActiveCohort : ULINT := 0; // Active cohort. Updated to the req. number from the sub system after request is confirmed.
    nRequestedCohort : ULINT := 1;
    rtToggle : R_TRIG;

	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
RequestedBP := IO_TO_BP(i_RequestedBP);

// Update arbiter with our request
rtToggle(CLK:= i_RequestedBP.xValid); // If interface resumes, reset/catch up
// xValid will go false if the PLC program on the other side is stopped. Ironically the ethercat interface continues happily.
IF i_RequestedBP.nCohortInt > nRequestedCohort OR rtToggle.Q THEN // If subsystem cohort has incremented, update beam parameter request with our arbiter
    Arbiter.RemoveRequest(RequestingSystemID);
    Arbiter.AddRequest(RequestingSystemID, RequestedBP);
    nRequestedCohort := i_RequestedBP.nCohortInt;
END_IF


// Check if latest request is acknowledged by this arbiter
// This happens after an arbitration
IF Arbiter.CheckRequest(RequestingSystemID) THEN
    nActiveCohort := nRequestedCohort; // If it is, then the active cohort is set to the requested cohort number.
END_IF

// Current beam params fanout
o_CurrentBP := BP_TO_IO(PMPS_GVL.stCurrentBeamParameters);
o_CurrentBP.nCohortInt := nActiveCohort;

// Both of these values should be 0 for a fully valid exchange
ffPMPSIO_Disconnect.i_xOK := NOT i_WcState AND NOT i_TxPDOState;

ffPMPSIO_Disconnect(
    io_fbFFHWO := fbFFHWO,
    i_xReset := Reset);]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>
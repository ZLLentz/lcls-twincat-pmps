<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_HardwareFFOutput" Id="{4c5345fc-1911-455c-a3d8-14bade73dcd8}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'reflection'}
FUNCTION_BLOCK FB_HardwareFFOutput
VAR_INPUT
    {attribute 'pytmc' := '
        pv: ClearFault
        io: o
     '}
    i_xReset    :    BOOL;
    {attribute 'pytmc' := '
        pv: EnableVeto
        io: o
     '}
    i_xVeto    :    BOOL;    
END_VAR
VAR_OUTPUT
    {attribute 'pytmc' := '
        pv: FaultHWO
        io: i
     '}
    q_xFastFaultOut    AT %Q*    :    BOOL;
END_VAR
VAR
    
    {attribute 'instance-path'}
    {attribute 'noinit'}
    sPath    :    T_MaxString;

	{attribute 'pytmc' := '
        pv: OK
        io: i
     '}
    xOK    :    BOOL; // Current internal state of FFO, indicates if FFO will accept a reset
    
    rtReset    :    R_TRIG;
    
    //Fault registration list
    //Fast faults add their name here to indicate they
    //"check in" to indicate their status with this fast
    //fault output. ie. They may be a culprit of beam off.
    asList    :    ARRAY[1..PMPS_GVL.MAX_FAST_FAULTS] OF T_MaxString;
    nIndex    :    UINT := 1;
        {attribute 'pytmc' := '
        pv: FF
     '}
    astFF : ARRAY[1..PMPS_GVL.MAX_FAST_FAULTS] OF ST_FF;
	EvalIdx: UINT := 1;
	IdxOK: BOOL;

END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[// <TODO> latch off beam if DI card status goes bad]]></ST>
    </Implementation>
    <Method Name="CheckIn" Id="{a62d20dc-2f83-47a3-8377-2fd77eff32f0}">
      <Declaration><![CDATA[METHOD CheckIn : BOOL
VAR_INPUT
    i_xOK    :    BOOL; //Keep true to keep beam
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT i_xOK THEN
    THIS^.xOK := FALSE; //Very clever, thanks Zach!
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Action Name="EvaluateOutput" Id="{1614665f-52f0-4071-a93e-c3d94919ee5b}">
      <Implementation>
        <ST><![CDATA[
IdxOK := True;
FOR EvalIdx := 1 TO PMPS_GVL.MAX_FAST_FAULTS DO
    IF NOT astFF[EvalIdx].Info.InUse THEN 
        CONTINUE;
    ELSE
        IdxOK := IdxOK AND astFF[EvalIdx].BeamPermitted;
    END_IF    
END_FOR

//<TODO> add diagnostic for success or failure of reset
rtReset(CLK:=i_xReset AND xOK);

q_xFastFaultOut S= rtReset.Q OR i_xVeto;
q_xFastFaultOut R= NOT xOK AND NOT i_xVeto;

//Reset OK for next cycle
xOK := True;]]></ST>
      </Implementation>
    </Action>
    <Method Name="IdxCheckIn" Id="{9a63f75f-7c14-46d0-b762-024d9e3e2e01}">
      <Declaration><![CDATA[METHOD IdxCheckIn : BOOL
VAR_INPUT
    Idx : UINT;
    OK : BOOL;
    Reset : BOOL;
END_VAR
VAR
    stFF : ST_FF;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[stFF := THIS^.astFF[LIMIT(1,Idx,PMPS_GVL.MAX_FAST_FAULTS)];

// Veto timer
stFF.Veto.Timer(
    IN := stFF.Veto.Activate,
    PT := stFF.Veto.Duration,
    Q => stFF.Veto.Active,
    ET => stFF.Veto.ElapsedTime);

// Updating internal fault state
stFF.OK := OK;

// Reset and latching logic
stFF.rtReset(CLK:=stFF.Reset OR Reset);
stFF.Reset R= stFF.rtReset.Q;
stFF.bsFF(SET := rtReset.Q, RESET1:= NOT OK);    

// Fault generation    
IF NOT OK AND NOT stFF.Veto.Active THEN
    THIS^.xOK := FALSE; //Very clever, thanks Zach!
END_IF

// Fast fault accumulation
stFF.ftCountFault(CLK:=stFF.bsFF.Q1);
IF stFF.ftCountFault.Q THEN PMPS_GVL.AccumulatedFF := PMPS_GVL.AccumulatedFF + 1; END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Register" Id="{23e16e56-a8bb-4023-a4d1-ddbd32f89678}">
      <Declaration><![CDATA[METHOD Register : BOOL
VAR_INPUT
    stFFInfo : ST_FFInfo := (
        sPath := '',
        Desc := '',
        TypeCode := 16#00,
        DevName := ''); // Fast fault information
END_VAR
VAR_OUTPUT
    FFOName : T_MaxString;
    Idx : UINT;
END_VAR
VAR
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[FFOName := THIS^.sPath;
Idx := THIS^.nIndex;

IF THIS^.nIndex <= PMPS_GVL.MAX_FAST_FAULTS THEN
    
    THIS^.astFF[MIN(THIS^.nIndex,PMPS_GVL.MAX_FAST_FAULTS)].Info:=stFFInfo;
    
    THIS^.nIndex := THIS^.nIndex + 1;
    
    Register := TRUE; // Add successful    
ELSE
    Register := FALSE; // Failed to add name to list
END_IF]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>